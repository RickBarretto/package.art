import ./{base}!

define :LicenseBadge [
    init: constructor [
        builder :type 
        owner :string 
        repository :string
        branch :string
    ]

    image: method [][
        src: "https://img.shields.io/github/license/{{owner}}/{{repository}}?"
            | replace "{{owner}}" this\owner
            | replace "{{repository}}" this\repository

        to :HtmlImage #[
            source: src
            description: "License"
        ]!
    ]

    string: method [][
        builder: this\builder
        url: "https://github.com/{{owner}}/{{repository}}/blob/{{branch}}/LICENSE"
            | replace "{{owner}}" this\owner
            | replace "{{repository}}" this\repository
            | replace "{{branch}}" this\branch

        image: this\image

        badge: to builder #[
            url: url
            image: image
        ]!

        to :string badge
    ]
]

license: $[owner :string repository :string][
    branch: (attr 'branch) ?? "main"

    badge: to :LicenseBadge #[
        builder: :Badge
        owner: owner
        repository: repository
        branch: branch
    ]!

    to :string badge
]

if standalone? ::

    inspect license "RickBarretto" "unitt"
    inspect license.branch: "master" "RickBarretto" "unitt"
