import ./{utils/meta}!

githubUrl: "https://github.com/"
isGithub?: $[x :string][ prefix? x githubUrl ]

getRepository: $[][
    url: input "Enter the repository URL:\n>>> "
    ensure -> isGithub? url 

    parts: url 
        | replace githubUrl ""
        | split.by: "/"

    owner: first parts
    repo: last parts

    to :Repository [owner repo]!
]

getPackage: $[repository :Repository][

    packageName: repository\name
    if suffix? packageName ".art"
        -> replace 'packageName ".art" ""

    order: "Enter the package name:"
    default: ~"Default: |packageName|"
    packageName: (input join.lines @[order default ">>> "]) ?? packageName

    description: (input "Enter the package description:\n>>> ") ?? "A package for Arturo"

    to :Package [packageName description]!
]